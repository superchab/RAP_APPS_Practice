@Metadata.layer: #CORE

@UI.headerInfo: { typeName: 'Task',
                  typeNamePlural: 'Tasks',
                  title: { type: #STANDARD, value: 'Title' },
                  description: { type: #STANDARD, value: 'TaskId' } }

annotate entity zzum_c_tasktp with

{
  @UI.facet: [
  //  Tab 1: General Ifnor
  { id: 'General',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Generela Information',
                 position: 10 },
  //  Tab 2: Subtasks ( The line item list )
               { id: 'Subtasks',
                 purpose: #STANDARD,
                 type: #LINEITEM_REFERENCE,
                 label: 'Subtasks',
                 position: 20,
                 targetElement: '_Subtask' } ]

  @UI.identification: [ { position: 10 } ]
  @UI.lineItem: [ { position: 10, importance: #HIGH } ]
  @UI.selectionField: [ { position: 10 } ]
  @EndUserText.label: 'Task ID'
  TaskId;

  @EndUserText.label: 'Title'
  @EndUserText.quickInfo: 'Title'
  @UI.identification: [ { position: 20 } ]
  @UI.lineItem: [ { position: 20, importance: #HIGH } ]
  @UI.selectionField: [ { position: 20 } ]
  Title;

  @EndUserText.label: 'Description'
  @EndUserText.quickInfo: 'Description'
  @UI.identification: [ { position: 30 } ]
  @UI.lineItem: [ { position: 30 } ]
  Description;

  @UI.identification: [ { position: 40, criticality: 'StatusCriticality', criticalityRepresentation: #WITHOUT_ICON } ]
  @UI.lineItem: [ { position: 40, criticality: 'StatusCriticality', criticalityRepresentation: #WITHOUT_ICON } ]
  @UI.textArrangement: #TEXT_LAST -- This ensures "I (In Progress)" fromat
  @UI.dataPoint: {
      qualifier: 'StatusHeader',
      title: 'Current Status',
      criticality: 'StatusCriticality',
      criticalityRepresentation: #WITHOUT_ICON
  }
  @Consumption.valueHelpDefinition: [{ entity.name: 'ZZUM_I_STATUS_VH', entity.element: 'Status' }]
  @EndUserText.label: 'Overall status'
  OverallStatus;

  @UI.hidden: true
  Locallastchangedat;

  @UI.hidden: true
  TaskUuid;
//  /* Associations */
  //  _Subtask;
}